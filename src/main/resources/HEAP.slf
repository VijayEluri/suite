-- Pairing heap
-- http://en.wikipedia.org/wiki/Pairing_heap

using external STANDARD >>
data (heap {:t}) over :t as Empty >>
data (heap {:t}) over :t as Heap (:t, [heap {:t}]) >>

define elem = (heap {:t} -> :t) of
	`Heap ($elem, $list)` => elem
>>
define children = (heap {:t} -> [heap {:t}]) of
	`Heap ($elem, $list)` => list
>>
define find-min =
	elem
>>
define meld = (heap0 => heap1 =>
	if (heap0 != Empty) then
		if (heap1 != Empty) then
			define elem0 = elem {heap0} >>
			define elem1 = elem {heap1} >>
			if (elem0 < elem1) then
				Heap (elem0, (heap1; children {heap0}))
			else
				Heap (elem1, (heap0; children {heap1}))
		else heap0
	else heap1
) >>
define add = (heap => elem =>
	meld {Heap (elem,)} {heap}
) >>
define delete-min = (heap =>
	define merge-pairs =
		case
		|| `$h0; $h1; $hs` => meld {meld {h0} {h1}} {merge-pairs {hs}}
		|| `$h;` => h
		|| anything => error
	>>
	merge-pairs {children {heap}}
) >>

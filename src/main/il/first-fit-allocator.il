constant ffa-node = dl-node struct (
	size as int,
);

declare ffa-sync = 0;
declare ffa-region0 = +x50000;
declare ffa-regionx = +xA0000;
declare ffa-current;

declare-pointer head to ffa-node;

declare ffa-prev = dl-prev;
declare ffa-next = dl-next;

declare ffa-allocate0 = function [size,] (
	declare next-block = function [block,] (
		declare n = ffa-next [block,];
		if (n != null) then n else head;
	);

	declare start = ffa-current;
	declare next;
	while ((let next = next-block [ffa-current,]) != start && `ffa-current`/current/size < size) do (
		let ffa-current = next;
	);

	declare diff = `ffa-current`/current/size - size;

	if (diff >= 0) then (
		if (diff >= 16) then (
			dl-insert-next [ffa-current, ffa-current +offset (size + 12),];
			let next = ffa-next [ffa-current,];
			let `next`/current/size = diff - 12;
		) else ();

		let `ffa-current`/current/size = 0;
		ffa-current +offset 12;
	) else null;
);

declare ffa-deallocate0 = function [block,] (
	declare prev = ffa-prev [block,];
	declare next = ffa-next [block,];

	if (prev != null && `prev`/current/size != 0) then (
		dl-remove [block,];
		let block = prev;
	) else ();

	if (next != null && `next`/current/size != 0) then (
		dl-remove [next,];
	) else ();

	let `block`/current/size = (ffa-next [block,]) as int - block as int - 12;
);

declare ffa-allocate = function [size,] (
	synchronized (& ffa-sync) (
		(ffa-allocate0 [size,]) as int + 12;
	);
);

declare ffa-deallocate = function [p, size as int,] (
	synchronized (& ffa-sync) (
		ffa-deallocate0 [(p - 12) as pointer:ffa-node,];
	);
);

declare ffa-setup = function [] (
	declare first = ffa-region0 as pointer:ffa-node;
	declare last = (ffa-regionx - 12) as pointer:ffa-node; -- Stopper record

	let `first`/current/size = ffa-regionx - ffa-region0 - 24;
	let `last`/current/size = 0;

	dl-new [head,];
	dl-insert-next [head, first,];
	dl-insert-next [first, last,];
	let ffa-current = head;
);
